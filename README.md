# Commainder
バッチファイル謹製リマインダー。`r 180 カップラーメン` で 3 分後に「カップラーメン」と表示。

## デモ
たとえばファイル名を指定して実行から、以下のようにして指定します。

![from_run](https://user-images.githubusercontent.com/23325839/47912109-d43ffc00-deda-11e8-9b72-24c560164126.png)

動作中は DOS 窓が動きますので、適当に最小化でもしておいてください。

![working](https://user-images.githubusercontent.com/23325839/47912160-03566d80-dedb-11e8-884e-3d06354ece6b.png)

時間になると、以下のように **最大化表示したメモ帳** でリマインドされます。

![remind](https://user-images.githubusercontent.com/23325839/47912131-e9b52600-deda-11e8-9535-6b504b75925f.png)

## 動作環境
- Windows 7 以降

## 使い方
- (1) `r.bat` を実行する
  - データフォルダが `%userprofile%\.commainder` 配下に生成されます
- (2) `r.bat` をパスの通ったフォルダに配置してください
  - てっとり早くやりたいなら `open_windows_folder_passed_to_PATH.bat` を実行し、開かれた Windows フォルダに配置します

すると「ファイル名を指定して実行」や「コマンドプロンプト」から r.bat を呼び出せるようになります。

呼び出し例:

- 3分後に「カップラーメン」と表示
  - → `r 180 カップラーメン`
- 3分後にリマインド（メッセージ内容を省略する）
  - → `r 180`

一般的なフォーマット:

- `r (秒数) (リマインド内容)`
- 秒数は省略すると 0 とみなされ、即座にリマインドされます。
- リマインド内容は省略できます。

## 技術的な話
Commainder の実装や疑問や不満など、技術的なことを Q&A 形式でまとめておきます。技術的に興味がある方向けです。ただ使うだけなら読む必要はありません。

### Q: なぜバッチファイル謹製？
Ans: ただの好奇心です。

バッチファイルだけでリマインダーを作る、というチャレンジが面白そうだなぁと。

### Q: なぜ「メモ帳を最大化して表示」という方法を選んだ？
Ans: なるべく確実にリマインド内容に気付かせるためです。

リマインドするためには「指定したメッセージを何らかの手段で表示」させることが必要ですが、Windows 標準機能だけでは、これを行える仕組みがありません。

- msg コマンドは？
  - ダイアログボックスが小さく、気づきにくいので却下しました
- Powershell や VBScript は？
  - 動作が重たいし、バッチファイルから外れるので却下しました

上手い表示方法が浮かばず、苦戦していたのですが、ふと「そうだ、リマインド内容をテキストに書いといて、それをメモ帳で開けば、気付きやすいんじゃないか」とひらめき、実装してみたところ、良い感じでしたので採用しました。

### Q: パスを通すために Windows フォルダに置かせるのはひどくないか？
Ans: 他に良い方法がありません。

詳しい方なら、既にパスを通したフォルダがあるでしょうから、勝手にそこへ置けば良いと思います。

そうでない場合、パスを通したフォルダを準備させるのは手間なので、「元から通っているフォルダに入れればいい」という指示にしました。Windows フォルダという、システムフォルダに入れるのは行儀がよろしくありませんが、元から通っているフォルダが他にないので、仕方ありません。

### Q: コマンドプロンプトから r を実行すると、タイトルが書き換えられてしまうが、戻してくれないの？
Ans: 戻せません。ご了承ください。

タイトルを戻すためには、「コマンドプロンプトに現在表示されている内容を取得する手段」が必要になりますが、これが存在しません。もし存在するのなら、ぜひ教えてほしいです。r.bat に実装します。

### Q: r.bat について解説してほしいです
Ans: v0.1.0 ベースで簡単に。

```bat
rem Comminder v0.1.0
rem ================
```

ただのバージョン情報表記です。rem コマンドはコメント構文であり、バッチファイル中にコメントを書くための命令です。

```bat
setlocal

...中略...

set n=%1
set msg=%*

set datafoldername=.comminder
set datafolderfull=%userprofile%\%datafoldername%
set dataext=.txt
```

データファイルの名前やパスなど、よく使う値を変数に入れています。よく使うデータを共通化するのはプログラミングの常識ですよね。

また、setlocal は、set による環境変数定義を呼び出し元に波及させないための決まり文句です。言うなれば、set はデフォルトではグローバル変数をセットしますが、setlocal を実行すると、ローカル変数をセットするようになる、というイメージです。

```bat
if not exist %datafolderfull% (
	echo 初回起動につき, データファイル保存先 "%datafolderfull%" を作成します.
	mkdir %datafolderfull%
)
```

データフォルダが存在しない場合、mkdir で新規作成しています。if not exist による判定も必要です。この判定がないと、データフォルダが存在する場合に mkdir で「既に作成されています」系のエラーが出てしまいます。

```bat
if "%n%"=="" (
	set n=0
)

if "%msg%"=="" (
	set msg=これは動作確認です。このように表示されます。
)
```

これは r.bat に渡される引数が無い場合の対処です。待機秒数の n は、数字を設定しないと待機命令である timeout コマンドがエラーを出すので、0 を設定します。リマインド内容の msg は、内容が空だと、データファイル書き込み時に使っている echo が「echo は <OFF> です」というメッセージを吐いてしまうため、これを防ぐために適当なメッセージ(動作確認のスタンスを取ってみました)を入れています。

```bat
set todaydate=%date%
set todaydate_for_disp=%todaydate%
set todaydate=%todaydate:/=%
set todaydate=%todaydate:~0,8%
set todaytime=%time%
set todaytime_for_disp=%todaytime%
set todaytime=%todaytime: =0%
set todaytime=%todaytime::=%
set todaytime=%todaytime:.=%
set todaytime=%todaytime:~0,8%
set todaydatetime=%todaydate%_%todaytime%
```

これは現在日付時刻の文字列を作っています。データファイル保存時、同じファイル名で保存してしまわないよう、被らないファイル名を付けることが必要ですが、こういう時は日付時刻を使うのがベターです。

バッチファイルでは環境変数 `%date%` で現在日付を、 `%time%` で現在時刻を取得できます。あとは、バッチファイルの文法で、上手いこと加工します。加工方法は `set /?` コマンドが詳しいです。

```bat
echo 【リマインド内容】 > %datafilefull%
echo %msg% >> %datafilefull%
echo.>> %datafilefull%
echo ======== >> %datafilefull%
echo.>> %datafilefull%
echo Set from: %todaydate_for_disp% %todaytime_for_disp%.  >> %datafilefull%
echo Wait sec: %n% Secs.  >> %datafilefull%
```

データファイルにリマインド内容を書き込んでいます。

まずは `echo >` でデータファイルを新規し、その後 `echo >>` で追記をしていっています。追記は最終行に追加されるので、上に書きたい内容ほど先に `echo >>` する必要があります。

```
title %n%秒後にリマインド
```

これは、r.bat 実行時に開かれるコマンドプロンプトウィンドウのタイトルを、見やすいよう変えているだけです。「ファイル名を指定して実行」から実行した場合は、上手いこと機能してくれます。コマンドプロンプトから実行した場合は、そのコマンドプロンプトのタイトルが置き換わってしまいます。上手い回避方法は無かったので見逃して下さい。

```
timeout /T %n% /nobreak
```

リマインダーの肝となる **指定時間になるまで待つ**　を実現するコマンドです。 `/nobreak` は、迂闊にキーを押した時に終了しないようにするためのオプションです。timeout コマンドは、デフォルトでは「何かキーを押したら（待ち時間が残っていても）即座に終了する」挙動なので、 /nobreak で無効にしないとうっかり終了してしまいかねません。

```
start "" /max notepad "%datafilefull%"
```

これはデータファイルをメモ帳で開くコマンドです。その際、`/max` オプションにて最大化で開く、という指令も与えています。start コマンドだからこそできることです。

## ライセンス
[MIT License](LICENSE)

## 作者
[stakiran](https://github.com/stakiran)
